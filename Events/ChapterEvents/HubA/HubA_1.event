EventPointerTable(HubA_1EventsID, PointerList)

#define CarmenIndioTalkSeen 0x7
#define LeandraTalkSeen 0x8
#define LeandraRecruited 0x9
#define RelicRecovered 0xA
#define ObjectiveSwitched 0xB
#define EscapeDisallowed 0xC

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, InformObjective, 1)
TurnEventEnemy(0, GeraldoLeaves, 1)
TurnEventPlayer(0, TimerWarning, 4)
TurnEventEnemy(0, SuperbossesAppear, 2)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(CarmenIndioTalkSeen, CarmenIndioTalk, Carmen, Indio)
CharacterEventBothWays(LeandraRecruited, LuaRecruitLeandra, Lua, Leandra)
CharacterEventBothWays(LeandraRecruited, DesiderioRecruitLeandra, Desiderio, Leandra)
END_MAIN

LocationBasedEvents:
Escape(EscapeDisallowed, EscapeEventCheck, 5, 2)
DurabilityChest(SkillScroll, CanterID, 17, 4)
ChestMoney(3000, 6, 15)
Chest(Bloodletter, 22, 15)
CHES RelicRecovered IronSword [18, 7] 0x14 // Macro can't set a flag so this is the objective chest 
END_MAIN

MiscBasedEvents:
AFEV ObjectiveSwitched SwitchObjective RelicRecovered
AFEV 0x0 CheckNoUnits 0x0
CauseGameOverIfLordDies
END_MAIN

Units1:
UNIT Indio Shaman 0 Level(1, Ally, False) [7,5] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [5,5] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [4,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [6,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [5,3] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [25,8] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [27,8] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [26,7] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [28,7] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT

LuaDesiderioCutsceneUnits:
UNIT Lua Captain 0 Level(1, Ally, False) [0,3] 0 0 1 REDA6R2 [0, 0, 0, 0] NoAI
UNIT Desiderio Vanguard 0 Level(1, Ally, False) [0,4] 0 0 1 REDA5R2 [0, 0, 0, 0] NoAI
UNIT

IndioCutsceneUnit:
UNIT Indio Shaman 0 Level(1, Ally, False) [0,4] 0 0 1 REDA4R2 [Surge, Vulnerary] NoAI
UNIT

ContractorInitialEnemies:
UNIT Leandra Fighter Geraldo Level(5, Enemy, False) [11, 5] 0 0 0 0 [Bloodaxe, HandAxe, Vulnerary] AttackInTwoAI
UNIT Geraldo Mercenary Geraldo Level(5, Enemy, False) [12, 5] 0 0 0 0 [IronSword] AttackInRangeAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [10, 4] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [12, 4] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [23, 10] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [23, 12] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT

SentinelInitialEnemies:
UNIT Buzu Vanguard Sanz Level(5, Enemy, False) [17, 7] 0 0 0 0 [HandAxe, Pike] BossAI
UNIT Bozo Warden Sanz Level(5, Enemy, False) [19, 7] 0 0 0 0 [FlameSword, Onigiri] BossAI
UNIT

SuperbossGang:
UNIT Yurena Champion Sanz Level(20, Enemy, False) [12, 21] 0 0 1 REDA12R20 [BraveLance, Saunion] BossAI
UNIT Sanz Sage Sanz Level(20, Enemy, False) [13, 21] 0 0 1 REDA13R20 [Tornado, Warp] BossAI
UNIT Orlando Captain Orlando Level(1, Enemy, False) [14, 21] 0 0 1 REDA14R20 [0] BossAI
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
EVBIT_F 8
LOAD2 1 LuaDesiderioCutsceneUnits
ENUN
Text(ChA1BeginningScene1)
CAMERA [26, 9]
CURSOR_FLASHING [26, 9]
CURSOR_FLASHING [27, 9]
STAL 60
REMOVE_CURSORS 
Text(ChA1BeginningScene2)
CAMERA [16, 17]
Text(ChA1BeginningScene3)
CAMERA Lua
LOAD1 0 IndioCutsceneUnit
ENUN
Text(ChA1BeginningScene4)
ENUT EscapeDisallowed
ENUT AllUnitsMustEscapeFlag
LOAD1 1 ContractorInitialEnemies
ENUN
LOAD1 1 SentinelInitialEnemies
ENUN
TILECHANGE 0x8
TILECHANGE 0x6
GotoPrepScreen
NoFade
ENDA

ALIGN 4
InformObjective:
CAMERA_CENTERED [18, 7]
CURSOR_FLASHING [18, 7]
STAL 60
REMOVE_CURSORS
Text(ChA1InformObjectiveDialogue)
NoFade
ENDA

ALIGN 4
CarmenIndioTalk:
Text(ChA1CarmenIndioTalkDialogue)
NoFade
ENDA

ALIGN 4
GeraldoLeaves:
CAMERA_CENTERED [11, 5]
CURSOR_FLASHING [11, 5]
STAL 60
REMOVE_CURSORS
Text(ChA1GeraldoLeavesDialogue)
MOVE 60 Geraldo [9, 4]
ENUN
DISA Geraldo
NoFade
ENDA

ALIGN 4
LuaRecruitLeandra:
CHECK_EVENTID LeandraTalkSeen
BNE 1 sC s0
Text(ChA1LuaLeandraTalkDialogue)
ENUT LeandraTalkSeen
LABEL 1
SVAL s1 3000
CHECK_MONEY
BLT 2 sC s1
Text(ChA1LeandraRecruitPrompt)
SVAL s1 0x1
BNE 2 s1 sC
Text(ChA1LeandraRecruitedDialogue)
SVAL s3 3000
TAKEMONEY
CUSA Leandra
GOTO 3
LABEL 2
ENUF LeandraRecruited
LABEL 3
NoFade
ENDA

ALIGN 4
DesiderioRecruitLeandra:
CHECK_EVENTID LeandraTalkSeen
BNE 4 sC s0
Text(ChA1DesiderioLeandraTalkDialogue)
ENUT LeandraTalkSeen
SVAL s1 3000
CHECK_MONEY
BLT 5 sC s1
LABEL 4
Text(ChA1LeandraRecruitPrompt)
SVAL s1 0x1
BNE 5 s1 sC
Text(ChA1LeandraRecruitedDialogue)
SVAL s3 3000
TAKEMONEY 
CUSA Leandra
GOTO 6
LABEL 5
ENUF LeandraRecruited
LABEL 6
NoFade
ENDA

ALIGN 4
TimerWarning:
Text(ChA1TimerWarningDialogue)
NoFade
ENDA

ALIGN 4
SuperbossesAppear:
CAMERA_CENTERED [13, 20]
STAL 30
LOAD1 1 SuperbossGang
ENUN
Text(ChA1SanzOrlandoAppearDialogue)
NoFade
ENDA

ALIGN 4
SwitchObjective:
Text(ChA1RelicRecoveredDialogue)
AddTrap(BlueSigilTrapID, 5, 2)
AddTrap(BlueSigilTrapID, 6, 2)
AddTrap(BlueSigilTrapID, 26, 8)
AddTrap(BlueSigilTrapID, 27, 8)
ENUF EscapeDisallowed
NoFade
ENDA

ALIGN 4
EscapeEventCheck:
ENUF EscapeDisallowed
NoFade
ENDA

ALIGN 4
CheckNoUnits:
ASMC GetBlueUnitCountASMC
BNE 1 s0 sC
ENUT 0x03
CALL EndingScene
LABEL 1
NoFade
ENDA

EndingScene:
NoFade
MNC2 HubA
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN