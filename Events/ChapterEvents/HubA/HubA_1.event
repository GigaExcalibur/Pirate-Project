EventPointerTable(HubA_1EventsID, PointerList)

#define CarmenIndioTalkSeen 0x7
#define LeandraRecruited 0x8
#define RelicRecovered 0x9
#define ObjectiveSwitched 0xA
#define EscapeDisallowed 0xB

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, InformObjective, 1)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Escape(EscapeDisallowed, EscapeEventCheck, 5, 2)
DurabilityChest(SkillScroll, CanterID, 17, 4)
ChestMoney(3000, 6, 15)
Chest(Bloodletter, 22, 15)
CHES RelicRecovered IronSword [18, 7] 0x14 // Macro can't set a flag so this is the objective chest 
END_MAIN

MiscBasedEvents:
AFEV ObjectiveSwitched SwitchObjective RelicRecovered
AFEV 0x0 CheckNoUnits 0x0
CauseGameOverIfLordDies
END_MAIN

Units1:
UNIT Indio Shaman 0 Level(1, Ally, False) [7,5] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [5,5] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [4,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [6,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [5,3] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [25,8] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [27,8] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [26,7] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [28,7] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT

LuaRazvanCutsceneUnits:
UNIT Lua Captain 0 Level(1, Ally, False) [0,3] 0 0 1 REDA6R2 [0, 0, 0, 0] NoAI
UNIT Razvan Curate 0 Level(1, Ally, False) [0,4] 0 0 1 REDA5R2 [0, 0, 0, 0] NoAI
UNIT

IndioCutsceneUnit:
UNIT Indio Shaman 0 Level(1, Ally, False) [0,4] 0 0 1 REDA4R2 [Surge, Vulnerary] NoAI
UNIT

ContractorInitialEnemies:
UNIT Leandra Fighter Geraldo Level(5, Enemy, False) [11, 5] 0 0 0 0 [Bloodaxe, HandAxe, Vulnerary] AttackInTwoAI
UNIT Geraldo Mercenary Geraldo Level(5, Enemy, False) [12, 5] 0 0 0 0 [SteelSword] AttackInRangeAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [10, 4] 0 0 0 0 [SteelSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, False) [12, 4] 0 0 0 0 [SteelSword] AttackInTwoAI
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
LOAD1 1 LuaRazvanCutsceneUnits
ENUN
Text(ChA1BeginningScene1)
CAMERA [26, 9]
CURSOR_FLASHING [26, 9]
CURSOR_FLASHING [27, 9]
STAL 60
REMOVE_CURSORS 
Text(ChA1BeginningScene2)
CAMERA [16, 17]
Text(ChA1BeginningScene3)
CAMERA Lua
LOAD1 0 IndioCutsceneUnit
ENUN
Text(ChA1BeginningScene4)
ENUT EscapeDisallowed
ENUT AllUnitsMustEscapeFlag
LOAD1 1 ContractorInitialEnemies
ENUN
TILECHANGE 0x8
TILECHANGE 0x6
GotoPrepScreen
NoFade
ENDA

InformObjective:
CAMERA_CENTERED [18, 7]
Text(ChA1BeginningScene5)
AddTrap(GreenSigilTrapID, 18, 7)
NoFade
ENDA

ALIGN 4
SwitchObjective:
AddTrap(BlueSigilTrapID, 5, 2)
AddTrap(BlueSigilTrapID, 6, 2)
AddTrap(BlueSigilTrapID, 26, 8)
AddTrap(BlueSigilTrapID, 27, 8)
ENUF EscapeDisallowed
NoFade
ENDA

EscapeEventCheck:
ENUF EscapeDisallowed
NoFade
ENDA

CheckNoUnits:
ASMC GetBlueUnitCountASMC
BNE 1 s0 sC
    ENUT 0x03
    CALL EndingScene
LABEL 1
NoFade
ENDA

EndingScene:
NoFade
MNC2 Chapter2
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN